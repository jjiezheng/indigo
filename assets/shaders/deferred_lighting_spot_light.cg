#include "standard.h"
#include "utils.h"

sampler2D NormalMap;
sampler2D DepthMap;
sampler2D ShadowMap;

uniform float4 ViewPosition;

uniform float4 LightDirection;
uniform float4 LightPosition;
uniform float3 LightColor;

uniform float LightInnerAngle;
uniform float LightOuterAngle;
uniform float LightDecay;

uniform float4x4 View;
uniform float4x4 ViewProjInv;
uniform float4x4 ProjInv;
uniform float4x4 LightViewProj;
uniform float4x4 WorldViewProj;
uniform float3x3 NormalMatrix;

struct VOutput {
      float4 position 			: POSITION;
      float4 screenPosition 	: TEXCOORD0;
};

VOutput vs(float4 position : POSITION) {
    VOutput OUT;
    OUT.position = mul(WorldViewProj, position);
    OUT.screenPosition = OUT.position;
    return OUT;
}

float4 ps(float4 position 		: POSITION,
		  float4 screenPosition : TEXCOORD0) : COLOR0 {

	float4 screenPositionHom = screenPosition / screenPosition.w;
	float2 texCoord = contract(screenPositionHom.xyz).xy;

	//------------------------------------------------------------
	// world position
	//------------------------------------------------------------

	float4 normalData = unpackNormal(NormalMap, texCoord);
	float3 normal = normalize(normalData.xyz);
	float diffusePower = normalData.w;

	float4 depthSpec = tex2D(DepthMap, texCoord);

	if (depthSpec.r == 1.0f) {
		return float4(0, 0, 0, 0);
	}

	float depth = depthSpec.x;
	float specularPower = depthSpec.z;
	float specularIntensity = depthSpec.w;

	float4 positionScreen = float4(screenPositionHom.xy, depthSpec.x, 1.0f);
	float4 positionWorldRaw = mul(ViewProjInv, positionScreen);
	float4 positionWorld = positionWorldRaw / positionWorldRaw.w;

	//------------------------------------------------------------
	// spot light
	//------------------------------------------------------------

	float distance = length(LightDirection);
  	distance = distance * distance;

	// diffuse
	float3 directionToLight = normalize(LightPosition.xyz - positionWorld.xyz);
	float lightDirectionDot = dot(normalize(LightDirection.xyz), -directionToLight);

	float diffuseStrength = 0.0f;

	float3 lightVector = normalize(-LightDirection.xyz);
  	float3 lightVectorViewSpace = normalize(mul(NormalMatrix, lightVector));

	float lightOuterCos = cos(LightOuterAngle);
	float lightInnerCos = cos(LightInnerAngle);	

	if (lightDirectionDot > lightOuterCos) {
		diffuseStrength = max(0.0f, saturate(dot(normal, normalize(lightVectorViewSpace))));
		diffuseStrength *= smoothstep(lightOuterCos, lightInnerCos, lightDirectionDot);	
	}

	if (lightDirectionDot > lightInnerCos) {
		diffuseStrength = max(0.0f, saturate(dot(normal, normalize(lightVectorViewSpace))));
	}
	
	float3 diffuseContribution = LightColor * diffusePower * diffuseStrength / distance;

	//specular
	float specularContribution = 0;

	if (diffuseStrength > 0) {
		float4 viewDirectionRaw = ViewPosition - positionWorld;
		float3 viewDirection = normalize(viewDirectionRaw.xyz);

		float3 halfVectorRaw = lightVector + viewDirection;
		float3 halfVector = normalize(mul(NormalMatrix, halfVectorRaw));

		float i = pow(saturate(dot(normal, halfVector)), specularPower);
		specularContribution = i * specularIntensity / distance;
	}

	//------------------------------------------------------------
	// shadows
	//------------------------------------------------------------
	float4 pixelPositionFromLightRaw = mul(LightViewProj, positionWorld);
	float3 pixelPositionFromLight = pixelPositionFromLightRaw.xyz / pixelPositionFromLightRaw.w;

	float2 shadowCoord = (pixelPositionFromLight.xy * 0.5f) + 0.5f;
	shadowCoord.y = 1.0f - shadowCoord.y;

	float3 moments = tex2D(ShadowMap, shadowCoord).xyz;

	float shadowLightContribution = 1.0f;

	if (moments.x < pixelPositionFromLight.z && shadowCoord.x > 0.0f && shadowCoord.x < 1.0f) {
		shadowLightContribution = ChebyshevUpperBound(moments.xy, pixelPositionFromLight.z, 0.0000005f);
		shadowLightContribution = ReduceLightBleeding(shadowLightContribution, 0.9f);
	}

	diffuseContribution *= shadowLightContribution;

	return float4(diffuseContribution, specularContribution);
} 
